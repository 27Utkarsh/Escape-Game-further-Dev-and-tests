plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

java.sourceCompatibility = 17
java.targetCompatibility = 17

mainClassName = 'com.badlogic.debugthugs.headless.HeadlessLauncher'
application.setMainClass(mainClassName)

sourceSets.test.resources.srcDirs += [ rootProject.file('assets').path ]

dependencies {
    implementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation project(':core')

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly  'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'org.mockito:mockito-core:5.11.0'
}

tasks.withType(Test) {
    systemProperty 'user.dir', project.rootDir 
    workingDir = project.rootDir
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
    }
    finalizedBy jacocoTestReport
}

task jacocoTestReport(type: JacocoReport, dependsOn: test) {
    def coreProject = project(':core')

    additionalSourceDirs.from(coreProject.sourceSets.main.allSource.srcDirs)
    additionalClassDirs.from(coreProject.sourceSets.main.output)
    
    sourceDirectories.from(files(sourceSets.main.allSource.srcDirs))
    classDirectories.from(files(sourceSets.main.output))
    executionData.from(files("${buildDir}/jacoco/test.exec"))

    reports {
        xml.required = true
        html.required = true
    }
}
